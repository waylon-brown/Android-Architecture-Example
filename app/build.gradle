apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

// Duplicate dependency and tools versions
def versions = [
        sdk: 27,
        support: "27.0.0",
        arch: "1.0.0-rc1",
        butterknife: "8.4.0",
        retrofit: "2.1.0",
        dagger: "2.9",
        leakCanary: "1.5"
]

kapt {
    generateStubs = true
}

android {
    compileSdkVersion versions.sdk
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.redditapp"
        minSdkVersion 15
        targetSdkVersion versions.sdk

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "Reddit (Debug)"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Reddit App"
        }
    }

    // TODO: use flavor dimensions
    flavorDimensions "default"
    productFlavors {
        online {
            dimension "default"
        }
        
        offline {
            dimension "default"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }

    dependencies {
        implementation fileTree(dir: 'libs', include: ['*.jar'])

        androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        testCompile 'junit:junit:4.12'
        
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        implementation 'com.github.WaylonBrown:LifecycleAwareRx:0.4'
//        compile project(":LifecycleAwareRx")

        // Google
        implementation "com.android.support:appcompat-v7:$versions.support"
        implementation "com.android.support:design:$versions.support"
        implementation "com.android.support:cardview-v7:$versions.support"
        implementation "com.android.support.constraint:constraint-layout:1.0.2"
        kapt 'com.android.databinding:compiler:3.0.0'
        // Architecture components
        implementation "android.arch.lifecycle:runtime:$versions.arch"
        implementation "android.arch.lifecycle:extensions:$versions.arch"
        implementation "android.arch.lifecycle:reactivestreams:$versions.arch"
        kapt "android.arch.lifecycle:compiler:$versions.arch"
        implementation "android.arch.persistence.room:runtime:$versions.arch"
        kapt "android.arch.persistence.room:compiler:$versions.arch"

        // TODO: remove for databinding
        implementation "com.jakewharton:butterknife:$versions.butterknife"
        kapt "com.jakewharton:butterknife-compiler:$versions.butterknife"

        implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
        implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit" // Retrofit2 + Moshi
        implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0' // Retrofit2 + RxJava2
        implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

        implementation 'com.squareup.moshi:moshi:1.3.1'

        implementation 'com.github.bumptech.glide:glide:3.7.0'

        // Fonts
        implementation 'uk.co.chrisjenx:calligraphy:2.2.0'
        implementation "com.mikepenz:iconics-core:2.8.2@aar"
        // List of icons: https://material.io/icons/
        implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'

        implementation 'io.reactivex.rxjava2:rxjava:2.0.2'
        implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

        implementation "com.google.dagger:dagger:$versions.dagger"
        kapt "com.google.dagger:dagger-compiler:$versions.dagger"

        implementation 'com.jakewharton.timber:timber:4.4.0'

        debugCompile "com.squareup.leakcanary:leakcanary-android:$versions.leakCanary"
        releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
        testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
    }
}